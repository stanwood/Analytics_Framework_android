apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
group = 'com.github.stanwood.Analytics_Framework_android'
version = project.ext.FrameworkVersion
android {
    compileSdkVersion project.ext.CompileSdkVersion

    defaultConfig {
        minSdkVersion project.ext.MinSdkVersion
        targetSdkVersion project.ext.TargetSdkVersion
        versionCode 1
        versionName project.ext.FrameworkVersion
    }
}

dependencies {
    implementation "com.android.support:support-annotations:$rootProject.ext.SupportLibVersion"
    implementation "com.android.support:appcompat-v7:$rootProject.ext.SupportLibVersion"
    api 'com.jakewharton.timber:timber:4.7.0'
    api project(':core')
    api project(':firebasetrackingprovider-shared')
    api project(':fabrictrackingprovider-shared')
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}