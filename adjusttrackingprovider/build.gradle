apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
group = 'com.github.stanwood.Analytics_Framework_android'
version = project.ext.FrameworkVersion
android {
    compileSdkVersion project.ext.CompileSdkVersion

    defaultConfig {
        minSdkVersion project.ext.MinSdkVersion
        targetSdkVersion project.ext.TargetSdkVersion
        versionCode 1
        versionName project.ext.FrameworkVersion
        consumerProguardFiles 'proguard-rules.pro'
    }
}

dependencies {
    implementation project(':core')
    implementation "com.android.support:support-annotations:$rootProject.ext.SupportLibVersion"
    implementation "com.google.android.gms:play-services-analytics:$rootProject.ext.PlayServicesAnalyticsVersion"
    implementation 'com.adjust.sdk:adjust-android:4.12.0'
    implementation 'com.android.installreferrer:installreferrer:1.0'
    api project(':adjusttrackingprovider-shared')
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
